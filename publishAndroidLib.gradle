apply plugin: 'maven-publish'
apply plugin: 'signing'


ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["devName"] = ''


File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
}


def PUBLISH_GROUP_ID = 'com.whatspos.sdk' //这里可以不是直接申请时候的groupId只要开头是就可以

def PUBLISH_ARTIFACT_ID = "emm-android-sdk"

def PUBLISH_VERSION = "1.0.0"

android {
    publishing {
        singleVariant('release') {
            withJavadocJar()
            withSourcesJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
            }

            groupId = PUBLISH_GROUP_ID
            artifactId = PUBLISH_ARTIFACT_ID
            version = PUBLISH_VERSION


            pom {
                name = 'emm-android-sdk'
                description = 'EMM Service Android SDK.'
                url ='https://github.com/PAXSTORE/emm-android-sdk'

                licenses {
                    license {
                        name ='The Apache License, Version 2.0'
                        url ='http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers { // 改成你自己的信息
                    developer {
                        name = devName
                        email ='paxstore-support@paxsz.com'
                    }
                }

                scm {
                    //修改成你的Git地址：
                    connection = 'scm:git:github.com/PAXSTORE/emm-android-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/PAXSTORE/emm-android-sdk.git'
                    //分支地址：
                    url = 'https://github.com/PAXSTORE/emm-android-sdk/tree/master'
                }

            }

        }
    }


    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = PUBLISH_VERSION.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}


signing {
    sign publishing.publications.release
}